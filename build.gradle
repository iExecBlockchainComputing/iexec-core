buildscript {
    ext {
        springBootVersion = '2.2.13.RELEASE'
    }
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id "org.sonarqube" version "2.7"
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'maven'
apply plugin: 'jacoco'

group = 'com.iexec.core'
sourceCompatibility = 11
targetCompatibility = 11


repositories {
    mavenCentral()
    jcenter()
    maven {
        url "https://nexus.iex.ec/repository/maven-public/"
    }

    maven { url "https://jitpack.io" }
}

configurations {
    springBootJar
    libraryJar
}

def LIBRARY_JAR_NAME = project.name + "-library"
jar {
    enabled = true
    baseName = LIBRARY_JAR_NAME
}

// the project will create two artifacts: one spring boot executable and one library
artifacts {
    springBootJar file: file('build/libs/' + project.name + '-' + version + '.jar')
    libraryJar file: file('build/libs/' + LIBRARY_JAR_NAME + '-' + version + '.jar')
}

ext {
    //https://github.com/spring-cloud/spring-cloud-openfeign/issues/171
    //Greenwich.SR2 is a patch for Greenwich.RELEASE
    springCloudVersion = 'Greenwich.SR2'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

dependencies {
    // iexec
    compile "com.iexec.common:iexec-common:$iexecCommonVersion"
    //compile files("../iexec-common/build/libs/iexec-common-${iexecCommonVersion}.jar")

    // spring
    compile("org.springframework.boot:spring-boot-starter") {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging' //required for Graylog
    }
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.springframework.boot:spring-boot-starter-data-mongodb"
    compile "org.springframework.boot:spring-boot-starter-websocket"
    compile "org.springframework.retry:spring-retry"
    compile "org.springframework.boot:spring-boot-starter-aop"
    compile "org.springframework.boot:spring-boot-starter-security"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.cloud:spring-cloud-starter-openfeign"

    // NoSuchMethodError: 'okhttp3.RequestBody okhttp3.RequestBody.create(java.lang.String, okhttp3.MediaType)'
    implementation 'com.squareup.okhttp3:okhttp:4.3.1' // Web3j issue: https://github.com/web3j/web3j/issues/1180
    // NoSuchMethodError: 'byte[] kotlin.collections.ArraysKt.copyInto(byte[], byte[], int, int, int)'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.3.50' // https://stackoverflow.com/a/57907899

    testCompile "org.springframework.boot:spring-boot-starter-test"

    // swagger
    compile "io.springfox:springfox-swagger2:2.9.2"
    compile "io.springfox:springfox-swagger-ui:2.9.2"

    // jason web token
    compile "io.jsonwebtoken:jjwt:0.7.0"

    // expiring map
    compile "net.jodah:expiringmap:0.5.8"

    // ipfs
    compile "com.github.ipfs:java-ipfs-http-client:1.2.3"

    //graylog
    compile 'biz.paluch.logging:logstash-gelf:1.5.1'

    compile 'io.micrometer:micrometer-registry-prometheus:1.1.5'

    // lombok
    compileOnly "org.projectlombok:lombok:1.18.2"
    annotationProcessor "org.projectlombok:lombok:1.18.2"

    // package cloud
    springBootJar "io.packagecloud.maven.wagon:maven-packagecloud-wagon:0.0.6"
    libraryJar "io.packagecloud.maven.wagon:maven-packagecloud-wagon:0.0.6"

    // mongock
    compile platform('com.github.cloudyrock.mongock:mongock-bom:4.2.7.BETA')
    compile 'com.github.cloudyrock.mongock:mongock-spring-v5'
    compile 'com.github.cloudyrock.mongock:mongodb-springdata-v2-driver'
}

jacoco {
    toolVersion = "0.8.3"
}

jacocoTestReport {
    reports.xml.enabled true
    reports.xml.destination file("${jacoco.reportsDir}/test/cov.xml")
}

// the test coverage report will be created just before the build
build.dependsOn jacocoTestReport

def gitBranch = 'git name-rev --name-only HEAD'.execute().text.trim()
def isMasterBranch = gitBranch == "master"
def isDevelopBranch = gitBranch == "develop"
def canUploadArchives = (isMasterBranch || isDevelopBranch ) && project.hasProperty("nexusUser") && project.hasProperty("nexusPassword")
def gitShortCommit = 'git rev-parse --short HEAD'.execute().text.trim()
def isSnapshotVersion = project.version.contains("SNAPSHOT")

project.ext.getNexusMaven = {
    def nexusMavenBase = "https://nexus.iex.ec/repository"
    if (isSnapshotVersion) {
        return nexusMavenBase + "/maven-snapshots/"
    } else {
        return nexusMavenBase + "/maven-releases/"
    }
}

uploadSpringBootJar {
    description 'Upload the executable jar version of the core'
    repositories.mavenDeployer {
        pom.groupId = project.group
        pom.artifactId = project.name
        pom.version = version
        configuration = configurations.springBootJar
        repository(url: getNexusMaven()) {
            authentication(userName: project.nexusUser, password: project.nexusPassword)
        }
    }
}

uploadLibraryJar {
    description 'Upload the library jar version of the core'
    repositories.mavenDeployer {
        pom.groupId = project.group
        pom.artifactId = LIBRARY_JAR_NAME
        pom.version = version
        configuration = configurations.libraryJar
        repository(url: getNexusMaven()) {
            authentication(userName: project.nexusUser, password: project.nexusPassword)
        }
    }
}

uploadLibraryJar.enabled = canUploadArchives
uploadSpringBootJar.enabled = canUploadArchives
uploadArchives.enabled = canUploadArchives

uploadArchives.dependsOn uploadLibraryJar
uploadArchives.dependsOn uploadSpringBootJar


import org.apache.tools.ant.filters.ReplaceTokens

// create the version controller for the core
task createVersion(type: Copy) {
    // delete old one
    delete 'src/main/java/com/iexec/core/utils/version/Version.java'
    // use and copy template to the new location
    from 'src/main/resources/Version.java.template'
    into 'src/main/java/com/iexec/core/utils/version/'

    rename { String fileName ->
        fileName.replace('.template', '')
    }
    // replace tokens in the template file
    filter(ReplaceTokens, tokens: [projectversion: "${version}".toString()])
}
// the createVersion task should be called before compileJava or the version service will not work
compileJava.dependsOn createVersion

def imageName = "nexus.iex.ec/iexec-core"

project.ext.getDockerImageNameFull = {
    def imageNameWithVersion = imageName + ":${version}"
    if (isSnapshotVersion) {
        return imageNameWithVersion + "-" + gitShortCommit
    } else {
        return imageNameWithVersion
    }
}

project.ext.getDockerImageNameShortCommit = {
    return imageName + ":" + gitShortCommit
}

task prepareDockerFile(type: Copy) {
    delete 'build/main/resources/'
    from 'Dockerfile'
    into 'build/main/resources/'
    filter(ReplaceTokens, tokens: [projectversion: "${version}".toString()])
}


task buildImage(type: Exec) {
    description 'Build a docker image for the core'
    commandLine("sh", "-c", "docker image build -f build/main/resources/Dockerfile -t " + getDockerImageNameFull() + " . &&" +
            "docker tag " + getDockerImageNameFull() + " " + imageName + ":dev")
}

buildImage.dependsOn prepareDockerFile
buildImage.enabled = (isMasterBranch || isDevelopBranch ) || project.hasProperty("forceDockerBuild")

task pushImage(type: Exec) {
    if (project.hasProperty("nexusUser") && project.hasProperty("nexusPassword")) {
        commandLine("sh", "-c", "docker login -u " + project.nexusUser + " -p " + project.nexusPassword + " nexus.iex.ec && " +
                "docker push " + getDockerImageNameFull() + " && " +
                "docker tag " + getDockerImageNameFull() + " " + getDockerImageNameShortCommit() + " && " +
                "docker push " + getDockerImageNameShortCommit() + " && " +
                "docker logout")
    } else {
        println "Credentials for DockerHub are missing, the images cannot be pushed"
    }
}

pushImage.dependsOn buildImage
pushImage.enabled = (isMasterBranch || isDevelopBranch ) && project.hasProperty("nexusUser") && project.hasProperty("nexusPassword")
